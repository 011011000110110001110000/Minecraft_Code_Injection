plugins {
    id 'java'
}

group = 'me.lollopollqo'
version = '1.0-SNAPSHOT'

def manifestVer = '1.0'
def agentClass = 'me.lollopollqo.sus.injection.agent.Agent'
def mainClass = 'me.lollopollqo.sus.injection.Main'

def manifestAttributes = [
        'Manifest-Version'       : manifestVer,
        'Created-By'             : "Gradle ${gradle.gradleVersion}",
        'Agent-Class'            : agentClass,
        'Can-Redefine-Classes'   : 'true',
        'Can-Retransform-Classes': 'true',
        'Main-Class'             : mainClass,
        'Permissions'            : 'all-permissions'
]


repositories {
    mavenCentral()
    maven {
        url 'https://maven.fabricmc.net/'
    }
}

jar {
    manifest {
        attributes(manifestAttributes)
    }
}

tasks.register('fatJar', Jar) {
    manifest {
        attributes(manifestAttributes)
    }

    archiveBaseName = rootProject.name
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } with jar
}

dependencies {
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation 'org.ow2.asm:asm:9.5'

    testImplementation platform('org.junit:junit-bom:5.9.2')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

test {
    useJUnitPlatform()
}